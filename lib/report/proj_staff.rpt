/*
 *	aegis - project change supervisor
 *	Copyright (C) 1994 Peter Miller.
 *	All rights reserved.
 *
 *	This program is free software; you can redistribute it and/or modify
 *	it under the terms of the GNU General Public License as published by
 *	the Free Software Foundation; either version 2 of the License, or
 *	(at your option) any later version.
 *
 *	This program is distributed in the hope that it will be useful,
 *	but WITHOUT ANY WARRANTY; without even the implied warranty of
 *	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *	GNU General Public License for more details.
 *
 *	You should have received a copy of the GNU General Public License
 *	along with this program; if not, write to the Free Software
 *	Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 *
 * MANIFEST: report the staff currently assigned to a project
 */

auto p, ps, name, staff;
p = project[project_name()];
title("The Project Staff Report", sprintf("Project \"%s\"", p.name));
columns
(
	{ name = "Login\n--------"; width = 8; },
	{ name = "Full Name\n-----------"; width = 20; padding = 2; },
	{ name = "Role\n------"; width = 9; },
	{ name = ""; width = 8; },
	{ name = ""; width = 10; },
	{ name = ""; width = 13; }
);
ps = p.state;
for (name in ps.developer)
	staff[name].developer = true;
for (name in ps.reviewer)
	staff[name].reviewer = true;
for (name in ps.integrator)
	staff[name].integrator = true;
for (name in ps.administrator)
	staff[name].administrator = true;
for (name in sort(keys(staff)))
{
	print
	(
		name,
		passwd[name].pw_comment,
		(staff[name].developer ? "developer" : ""),
		(staff[name].reviewer ? "reviewer" : ""),
		(staff[name].integrator ? "integrator" : ""),
		(staff[name].administrator ? "administrator" : "")
	);
}
