/*
 *	aegis - project change supervisor
 *	Copyright (C) 1997, 1998, 2002 Peter Miller;
 *	All rights reserved.
 *
 *	This program is free software; you can redistribute it and/or modify
 *	it under the terms of the GNU General Public License as published by
 *	the Free Software Foundation; either version 2 of the License, or
 *	(at your option) any later version.
 *
 *	This program is distributed in the hope that it will be useful,
 *	but WITHOUT ANY WARRANTY; without even the implied warranty of
 *	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *	GNU General Public License for more details.
 *
 *	You should have received a copy of the GNU General Public License
 *	along with this program; if not, write to the Free Software
 *	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111, USA.
 *
 * MANIFEST: report the names of projects
 */

columns({width=1000;});
print("Content-Type: text/html");
print("");
print("<html><head><title>");
print("Project List");
print("</title></head><body bgcolor=\"#ffffff\"><h1 align=center>");
print("Project List");
print("</h1><table align=center>");

auto colors;
colors[0] = '#CCCCCC';
colors[1] = '#FFFFFF';
print("<tr bgcolor=\"" ## colors[0] ##
	"\"><th>Project</th><th>Description</th></tr>");

auto script_name;
script_name = getenv("SCRIPT_NAME");
if (script_name == "")
	script_name = "http://localhost/cgi-bin/aegis.cgi";

auto name, p, prev_proj, color, odd;
prev_proj = "";
color = 0;
for (name in sort(keys(project)))
{
	p = project[name];
	auto a;
	a = split(name, ".");
	if (a[0] != prev_proj)
	{
		color = (color + 1) % 2;
		prev_proj = a[0];
	}

	/*
	 * Reading the p.state value (it is deferred) may result
	 * in ``permision denied'' so we need to catch the
	 * exception as it goes past.  Use the error as the
	 * description.
	 */
	auto description, ok;
	ok = true;
	try
		description = quote_html(p.state.description);
	catch (description)
	{
		description = "<i>" ## quote_html(description) ## "</i>";
		ok = false;
	}
	print("<tr bgcolor=\"" ## colors[color] ##"\"><td valign=top>");
	if (ok)
	{
		auto href;

		href = script_name ## "?file@proj_menu";
		href ##= "+project@" ## quote_url(name);
		print("<a href=\"" ## href ## "\">");
	}
	print(quote_html(name));
	if (ok)
		print("</a>");
	print("</td><td valign=top>");
	auto part;
	for (part in wrap_html(description, 80))
		print(part);
	print("<br><i>");
	for (part in wrap(p.directory, 15))
		print(quote_html(part));
	print("</i></td></tr>");
}

print("</table>");
print("Listed " ## count(keys(project)) ## " projects.");
print("<hr>A similar report may be obtained from the command line, with");
print("<blockquote><tt>ael p</tt></blockquote>");
print("<hr>");
print("This page was generated " ## now() ## ".");
print("</body></html>");
