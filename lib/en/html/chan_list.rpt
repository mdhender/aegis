/*
 *	aegis - project change supervisor
 *	Copyright (C) 1997-2002 Peter Miller;
 *	All rights reserved.
 *
 *	This program is free software; you can redistribute it and/or modify
 *	it under the terms of the GNU General Public License as published by
 *	the Free Software Foundation; either version 2 of the License, or
 *	(at your option) any later version.
 *
 *	This program is distributed in the hope that it will be useful,
 *	but WITHOUT ANY WARRANTY; without even the implied warranty of
 *	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *	GNU General Public License for more details.
 *
 *	You should have received a copy of the GNU General Public License
 *	along with this program; if not, write to the Free Software
 *	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111, USA.
 *
 * MANIFEST: report the changes in a project
 */

auto script_name;
script_name = getenv("SCRIPT_NAME");
if (script_name == "")
    script_name = "http://localhost/cgi-bin/aegis.cgi";
auto pn;
pn = project_name();
auto href;
href = script_name ## "?file@proj_menu+project@" ## quote_url(pn);

columns({width = 1000; });
print("Content-Type: text/html");
print("");
print("<html><head><title>");
print("Project " ## quote_html(pn) ## " Changes");
print("</title></head><body bgcolor=\"#FFFFFF\"><h1 align=center>");
print("Project ``<a href=\"" ## href ## "\">" ## quote_html(pn) ##
	"</a>'',<br>");
print("List of Changes");
print("</h1>");
print("<table align=center>");

print("<tr bgcolor=\"#CCCCCC\"><th>Change</th><th>State</th>" ##
	"<th>Description</th><th>Download</th></tr>");

auto ps, cn, rownum;
ps = project[pn].state.branch;
rownum = 0;
for (cn in sort(keys(ps.change)))
{
    auto cs;
    cs = ps.change[cn];
    if (rownum++ % 6 < 3)
	print("<tr>");
    else
	print("<tr bgcolor=\"#CCCCCC\">");
    print("<td valign=top align=right>");
    auto junk;
    href = "file@chan_menu+project@" ## quote_url(pn) ## "+change@" ## cn;
    href = script_name ## "?" ## href;
    print("<a href=\"" ## href ## "\">" ## cn ## "</a>");
    print("</td><td valign=top>");
    print(cs.state);
    if
    (
	(cs.state == being_developed || cs.state == being_integrated)
    &&
       	!count(keys(cs.branch))
    )
    {
       	auto who, hi, h, email, long_name;
       	for (hi = count(cs.history) - 1; hi >= 0; --hi)
       	{
	    h = cs.history[hi];
	    if
	    (
	     	h.what == develop_begin
	    ||
	     	h.what == develop_end
	    ||
	     	h.what == integrate_begin
	    ||
	     	h.what == integrate_pass
	    )
	    {
	     	who = h.who;
	     	email = h.who;
	     	try { email = user[h.who].email_address; }
	     	catch (email) { email = ""; }
	     	if (email == "")
		    email = h.who;
	     	href = "mailto:" ## email;
	     	long_name = h.who;
	     	try { long_name = passwd[who].pw_comment; }
	     	catch (long_name) { long_name = h.who; }
	     	print("<br><a href=\"" ## href ## "\"><i>" ##
		    quote_html(long_name) ## "</i></a>");
       		break;
	    }
       	}
    }
    print("</td><td valign=top>");
    auto part;
    for (part in wrap_html(quote_html(cs.brief_description), 80))
       	print(part);

    /*
     * The download column.
     */
    print("</td><td valign=top>");
    if (cs.state != awaiting_development)
    {
	href = script_name ## "/" ## quote_url(pn) ## ".C" ## cn
	    ## ".ae?file@aedist+project@" ## quote_url(pn)
	    ## "+change@" ## cn;
	print("<a href=\"" ## href ## "\"><i>aedist</i></a>,");

	if (cs.state != being_developed || !count(keys(cs.branch)))
	{
	    href = script_name ## "/" ## quote_url(pn) ## ".C" ## cn
		## ".patch?file@aepatch+project@" ## quote_url(pn)
		## "+change@" ## cn;
	    print("<a href=\"" ## href ## "\"><i>patch</i></a>");
	}
    }
    print("</td></tr>");
}
print("</table>");
print("Listed " ## count(keys(ps.change)) ## " changes.");
print("<hr>A similar report may be obtained from the command line, with");
print("<blockquote><tt>ael c -p " ## quote_html(pn) ## "</tt></blockquote>");
print("<hr>");
print("If you download via the <i>aedist</i> links, you will need");
print("<i>aedist</i>(1) from Aegis 4.3 or later to apply the");
print("change sets.");
print("<p>");
print("If you download via the <i>patch</i> links, they can be applied");
print("with <i>aepatch</i>(1) from Aegis 3.28 or later.");
print("To apply them manually, you must first MIME64 decode and gunzip,");
print("before using the normal <i>patch</i>(1) program.");
print("Depending on your browser, it may or may not have done the MIME64");
print("and/or gunzip steps for you automagically.");
print("<hr>");
print("This page was generated " ## now() ## ".");
print("</body></html>");
