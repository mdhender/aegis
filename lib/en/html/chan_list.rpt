/*
 *	aegis - project change supervisor
 *	Copyright (C) 1997, 1998, 1999 Peter Miller;
 *	All rights reserved.
 *
 *	This program is free software; you can redistribute it and/or modify
 *	it under the terms of the GNU General Public License as published by
 *	the Free Software Foundation; either version 2 of the License, or
 *	(at your option) any later version.
 *
 *	This program is distributed in the hope that it will be useful,
 *	but WITHOUT ANY WARRANTY; without even the implied warranty of
 *	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *	GNU General Public License for more details.
 *
 *	You should have received a copy of the GNU General Public License
 *	along with this program; if not, write to the Free Software
 *	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111, USA.
 *
 * MANIFEST: report the changes in a project
 */

columns({width = 1000; });
print("Content-Type: text/html");
print("");
print("<html><head><title>");
auto pn;
pn = project_name();
print("Project " ## quote_html(pn) ## " Changes"); 
print("</title></head><body><h1><center>");
print("Project ``" ## quote_html(pn) ## "'',<br>");
print("List of Changes"); 
print("</center></h1>");
print("<center><table>");

print("<tr><th>Change</th><th>State</th><th>Description</th></tr>");

auto script_name;
script_name = getenv("SCRIPT_NAME");
if (script_name == "")
	script_name = "http://localhost/cgi-bin/aegis.cgi";

auto ps, cn;
ps = project[pn].state.branch;
for (cn in sort(keys(ps.change)))
{
	auto cs;
	cs = ps.change[cn];
	print("<tr><td valign=top width=\"10%\" align=right>");
	auto href, junk;
	href = "file@chan_menu+project@" ## quote_url(pn) ## "+change@" ## cn;
	try
	{
		if (count(keys(cs.branch)))
			href = "file@proj_menu+project@" ## quote_url(pn) ## "." ## cn;
	}
	catch (junk)
		;
	href = script_name ## "?" ## href;
	print("<a href=\"" ## href ## "\">" ## cn ## "</a>");
	print("</td><td valign=top width=\"20%\">");
	print(cs.state);
	if (cs.state == completed || count(keys(cs.branch)))
	{
		href = "file@aedist+project@" ## quote_url(pn)
			## "+change@" ## cn;
		href = script_name ## "?" ## href;
		print("<br><a href=\"" ## href ## "\"><i>(Download)</i></a>");
	}
	if
	(
		(cs.state == being_developed || cs.state == being_integrated)
	&&
		!count(keys(cs.branch))
	)
	{
		auto who, hi, h;
		for (hi = count(cs.history) - 1; hi >= 0; --hi)
		{
			h = cs.history[hi];
			if
			(
				h.what == develop_begin
			||
				h.what == develop_end
			||
				h.what == integrate_begin
			||
				h.what == integrate_pass
			)
			{
				who = h.who;
				href = "mailto:" ## who;
				print("<br><a href=\"" ## href ## "\"><i>" ##
					quote_html(passwd[who].pw_comment) ##
					"</i></a>");
				break;
			}
		}
	}
	print("</td><td width=\"65%\" valign=top>");
	auto part;
	for (part in wrap_html(quote_html(cs.brief_description), 80))
		print(part);
	print("</td></tr>");
}
print("</table></center>");
print("Listed " ## count(keys(ps.change)) ## " changes.");
print("<hr>A similar report may be obtained from the command line, with");
print("<blockquote><tt>ael c -p " ## quote_html(pn) ## "</tt></blockquote>");
print("<hr>");
print("This page was generated " ## now() ## ".");
print("</body></html>");
